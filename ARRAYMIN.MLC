		 TITLE 'ARRAY MINIMUM'
ARRAYMIN CSECT
**********************************************************************
* AUTHOR:		CIS 227 SPRING 2021
* DESCRIPTION:	FINDS THE MINIMUM OF THE ELEMENTS IN AN ARRAY
* STATUS:		????
**********************************************************************
*
R0		 EQU   0			'INDEX' REGISTER FOR BALR INSTRUCTION
R2       EQU   2			R2 = LOOP CONTROL REGISTER		
R3       EQU   3			R3 = MINIMUM VALUE FOUND IN THE ARRAY
R4       EQU   4			R4 = ARRAY INDEX REGISTER
R12	     EQU   12			BASE REGISTER
R13      EQU   13		    SAVE AREA POINTER
R14		 EQU   14 			RETURN REGISTER
*
* STANDARD ENTRY AND INITIALIZATION:
         STM   R14,R12,12(R13) SAVE REGISTERS
		 BALR  R12,R0		LOAD BASE REGISTER
		 USING BASE,R12		DECLARE BASE ADDRESS AND REGISTER
BASE	 ST    R13,SAVE+4   SAVE R13
		 LA	   R13,SAVE		R13 = ADDRESS OF SAVE AREA
* START:
*
         LA    R2,(ARRAYEND-ARRAY)/4-1 ARRAY SIZE - 1 ITERATIONS
		 L     R3,ARRAY		INITIALIZE MINIMUM WITH FIRST ELEMENT
		 LA    R4,4			INITIALIZE INDEX TO POINT TO 2ND ELEMENT
LOOP     C     R3,ARRAY(R4) COMPARE MINIMUM WITH NEXT ELEMENT
		 BNH   KEEPMIN      IF R3 <= NEXT ELEMENT, NO CHANGE NEEDED
		 L     R3,ARRAY(R4) ELSE R3 = NEW MINIMUM
KEEPMIN  LA    R4,4(R4)		INCREMENT INDEX REGISTER FOR NEXT ELEMENT
		 BCT   R2,LOOP		IF MORE ELEMENTS, REPEAT LOOP
		 ST    R3,MINIMUM   ELSE, SAVE MINIMUM AND EXIT
*
* STANDARD EXIT:			
DONE     L     R13,SAVE+4   RESTORE R13
		 LM    R14,R12,12(R13)  RESTORE REGISTERS
		 BR    R14			RETURN
* STORAGE:
ARRAY    DC    F'-1'		ARRAY OF NUMBERS
         DC    F'0'
         DC    F'0'
         DC    F'0'
         DC    F'-2'
ARRAYEND DS    0F			END OF ARRAY LABEL
MINIMUM  DS    F			MINIMUM VALUE FOUND IN THE ARRAY
SAVE	 DS    18F			SAVE AREA
		 END   ARRAYMIN